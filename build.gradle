
defaultTasks 'eclipse', 'compileProto', 'createDeploy'

apply plugin: 'java'

subprojects {
	defaultTasks 'build'

	apply plugin: 'java'
	apply plugin: 'eclipse'
	
	sourceCompatibility = 1.7
	targetCompatibility = 1.7
	
	version = '0.31-SNAPSHOT'
	ext.isReleaseVersion = !version.endsWith("SNAPSHOT")

	group = 'org.lumongo'


	repositories {
		mavenCentral()
		maven {
			url 'http://people.apache.org/~anshum/staging_area/lucene-solr-5.0.0-RC1-rev1654615/lucene/maven/'
		}
	}

	
	test {
		
		systemProperty 'mongoServer', 'localhost'
		systemProperty 'mongoPort', '27017'
		
		//tweaking memory settings for the forked vm that runs tests
		jvmArgs '-Xmx1500m'
		
		beforeTest { descriptor ->
			logger.lifecycle("\n\nRunning test: " + descriptor)
		}
		
		afterTest { descriptor ->
			logger.lifecycle("\nFinished test: " + descriptor + "\n\n")
		}
		
		onOutput { descriptor, event ->
			logger.lifecycle(event.message.trim())
		}
		workingDir = 'build/'
	}
	


	dependencies {
		testCompile 'junit:junit:[4.10,)'
	}

	javadoc {
		options.addStringOption("sourcepath", "src/main/java")
		exclude '**/schema/**'
	}
	
	sourceSets {
		main {
			resources {
				srcDir 'src/main/java'
			}
		}
		test {
			resources {
				srcDir 'src/test/java'
			} 
		}
	}

	task sourcesJar(type: Jar, dependsOn:classes) {
		classifier = 'sources'
		from sourceSets.main.allSource
	}
	
	artifacts {
		archives jar
		archives sourcesJar
	}
	
	compileJava {
		options.debug = true
		options.debugOptions.debugLevel = "source,lines,vars"
	}

}

 
