
defaultTasks 'eclipse', 'compileProto', 'createDeploy'

subprojects {
	apply plugin: 'java'
	apply plugin: 'eclipse'
	
	sourceCompatibility = 1.7
	targetCompatibility = 1.7
	
	version = '0.25'
	ext.isReleaseVersion = !version.endsWith("SNAPSHOT")

	group = 'org.lumongo'
	
	repositories {
		mavenCentral()
	}
	
	test {
		useTestNG()
		
		systemProperty 'mongoServer', 'localhost'
		systemProperty 'mongoPort', '27017'
		
		//tweaking memory settings for the forked vm that runs tests
		jvmArgs '-Xmx512m'
		
		beforeTest { descriptor ->
			logger.lifecycle("\n\nRunning test: " + descriptor)
		}
		
		afterTest { descriptor ->
			logger.lifecycle("\nFinished test: " + descriptor + "\n\n")
		}
		
		onOutput { descriptor, event ->
			logger.lifecycle(event.message.trim())
		}
		workingDir = 'build/'
	}
	
	dependencies {
		testCompile 'org.testng:testng:5.10:jdk15'
	}

	javadoc {
		options.addStringOption("sourcepath", "src/main/java")
	}
	
	sourceSets {
		main {
			resources {
				srcDir 'src/main/java'
			}
		}
		test {
			resources {
				srcDir 'src/test/java'
			} 
		}
	}

	task sourcesJar(type: Jar, dependsOn:classes) {
		classifier = 'sources'
		from sourceSets.main.allSource
	}
	
	artifacts {
		archives jar
		archives sourcesJar
	}
	
	compileJava {
		options.debug = true
		options.debugOptions.debugLevel = "source,lines,vars"
	}

}
